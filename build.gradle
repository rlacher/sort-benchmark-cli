plugins
{
    id 'application'
    id 'java'
    id 'jacoco'
}

repositories
{
    mavenCentral() 
}

dependencies
{
    implementation 'info.picocli:picocli:4.7.+'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.+'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.+'
    testImplementation 'org.mockito:mockito-core:5.17.+'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application
{
    mainClass = 'com.github.rlacher.sortbench.MainCommand'
}

tasks.named('clean')
{
    delete "$projectDir/docs/api"
}

tasks.named('javadoc')
{
    destinationDir = file("$projectDir/docs/api")
    title= "Sort Benchmark API"
    options.encoding = 'UTF-8'
    options.windowTitle = title
    options.header = title
    options.addStringOption('Xwerror', '-Xdoclint:all')
}

tasks.named('jar')
{
    archiveBaseName.set("sort-benchmark-cli")

    manifest
    {
        attributes 'Main-Class': 'com.github.rlacher.sortbench.MainCommand'
    }
}

tasks.named('run')
{
    standardInput = System.in
    jvmArgs '-Djava.util.logging.config.file=logging.properties'
    args '--sizes', 5_000, 10_000, 20_000
}

tasks.named('test')
{
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    testLogging
    {
        afterSuite { desc, result ->
            if (!desc.parent)
            {
                println "${result.testCount} tests completed, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
            }
        }
    }
}

task copyLibs(type: Copy)
{
    from configurations.runtimeClasspath
    into 'build/libs/lib'
}

assemble.dependsOn copyLibs
build.dependsOn copyLibs
